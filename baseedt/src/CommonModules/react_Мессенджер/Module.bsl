
Функция ПолучитьПользователей(ЗапросHTTP) Экспорт
	
	Исключить = ЗапросHTTP.ПараметрыЗапроса.Получить("excludedUser");	
	
	Возврат ПолучитьПользователейВJSON(Исключить);
	
КонецФункции

Функция ПолучитьПользователейВJSON(uuid) Экспорт
	
	УникальныйИдентификатор = Новый УникальныйИдентификатор(uuid);
	ТекущийПользователь = Справочники.Пользователи.ПолучитьСсылку(УникальныйИдентификатор);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТекущийПользователь", ТекущийПользователь); 
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Пользователи.Ссылка КАК id,
		|	Пользователи.Наименование КАК name,
		|	ПользователиКонтактнаяИнформация.Представление КАК email,
		|	Пользователи.СсылкаНаФотографию КАК PhotoUrl,
		|	""Member"" КАК role,
		|	Пользователи.Комментарий КАК info,
		|	""Добро пожаловать в чат!"" КАК welcomeMessage
		|ИЗ
		|	Справочник.Пользователи.КонтактнаяИнформация КАК ПользователиКонтактнаяИнформация
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
		|		ПО ПользователиКонтактнаяИнформация.Ссылка = Пользователи.Ссылка
		|ГДЕ
		|	ПользователиКонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.АдресЭлектроннойПочты)
		|	И НЕ Пользователи.Ссылка = &ТекущийПользователь";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	МассивПользователей = Новый Массив;
	СтруктураПользователей = Новый Структура;
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Пользователь = Новый Структура;
		Пользователь.Вставить("id", XMLСтрока(ВыборкаДетальныеЗаписи.id));
		Пользователь.Вставить("name", ВыборкаДетальныеЗаписи.name);
		Пользователь.Вставить("email", ВыборкаДетальныеЗаписи.email);
		Пользователь.Вставить("photoUrl", ВыборкаДетальныеЗаписи.PhotoUrl);
		Пользователь.Вставить("role", ВыборкаДетальныеЗаписи.role);
		Пользователь.Вставить("info", ВыборкаДетальныеЗаписи.info);
		Пользователь.Вставить("welcomeMessage", ВыборкаДетальныеЗаписи.welcomeMessage);
		
		МассивПользователей.Добавить(Пользователь);
	КонецЦикла;
	
	СтруктураПользователей.Вставить("users", МассивПользователей);
	
	Возврат react_ОбщегоНазначения.МассивСтруктурВJSON(СтруктураПользователей);
	
КонецФункции

