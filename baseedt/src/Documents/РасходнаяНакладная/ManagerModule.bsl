
Функция СформироватьРасходнуюНакладную(Ссылка) Экспорт
	
	ТабДок = Новый ТабличныйДокумент;
	Макет = Документы.РасходнаяНакладная.ПолучитьМакет("ПФ_MXL_Накладная");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РасходнаяНакладная.Дата,
	|	РасходнаяНакладная.ЗаказКлиента.Представление КАК ПредставлениеЗаказа,
	|	РасходнаяНакладная.Контрагент.Представление КАК ПредставлениеПолучателя,
	|	РасходнаяНакладная.Номер,
	|	РасходнаяНакладная.Организация.Представление КАК ПредставлениеПоставщика,
	|	РасходнаяНакладная.Склад,
	|	РасходнаяНакладная.Событие,
	|	РасходнаяНакладная.Товары.(
	|		НомерСтроки,
	|		Номенклатура.Код КАК Код,
	|		Номенклатура КАК Запас,
	|		Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		Количество,
	|		Цена,
	|		Сумма
	|	)
	|ИЗ
	|	Документ.РасходнаяНакладная КАК РасходнаяНакладная
	|ГДЕ
	|	РасходнаяНакладная.Ссылка В (&Ссылка)";
	
	Запрос.Параметры.Вставить("Ссылка", Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();

	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьПоставщик = Макет.ПолучитьОбласть("Поставщик");
	ОбластьПокупатель = Макет.ПолучитьОбласть("Покупатель");
	
	ОбластьПредставлениеЗаказа = Макет.ПолучитьОбласть("Заказ");
	ОбластьСклад = Макет.ПолучитьОбласть("Склад");
	
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьТовары = Макет.ПолучитьОбласть("Строка");
	
	ОбластьИтого = Макет.ПолучитьОбласть("Итого");
	ОбластьИтоговаяСтрока = Макет.ПолучитьОбласть("СуммаПрописью");
	ОбластьПодписи = Макет.ПолучитьОбласть("Подписи");
		
	СуммаИтого = 0;
	КоличествоПозиций = 0;
	ВставлятьРазделительСтраниц = Ложь;
	Пока Выборка.Следующий() Цикл
		Если ВставлятьРазделительСтраниц Тогда
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ОбластьЗаголовок.Параметры.Заполнить(Новый Структура("ТекстЗаголовка", "Расходная накладная № "
														+ Выборка.Номер
														+ " от "
														+ Формат(Выборка.Дата, "ДЛФ=DD")));
		
		ТабДок.Вывести(ОбластьЗаголовок);
		
		ОбластьПоставщик.Параметры.Заполнить(Выборка);
		ОбластьПокупатель.Параметры.Заполнить(Выборка);
		
		ОбластьПредставлениеЗаказа.Параметры.Заполнить(Выборка);
		ОбластьСклад.Параметры.Заполнить(Выборка);
		
		ТабДок.Вывести(ОбластьПоставщик);
		ТабДок.Вывести(ОбластьПокупатель);
		
		ТабДок.Вывести(ОбластьПредставлениеЗаказа);
		ТабДок.Вывести(ОбластьСклад);
		
		ТабДок.Вывести(ОбластьШапкаТаблицы);
		ВыборкаТовары = Выборка.Товары.Выбрать();
		Пока ВыборкаТовары.Следующий() Цикл
			ОбластьТовары.Параметры.Заполнить(ВыборкаТовары);
			ТабДок.Вывести(ОбластьТовары, ВыборкаТовары.Уровень());
			СуммаИтого = СуммаИтого + ВыборкаТовары.Сумма;
			КоличествоПозиций = КоличествоПозиций + 1;
		КонецЦикла;
		
		ОбластьИтого.Параметры.Заполнить(Новый Структура("Всего", СуммаИтого));
		
		ТабДок.Вывести(ОбластьИтого);
		
		СуммаПрописью = ЧислоПрописью(СуммаИтого) + " тенге";
		ИтоговаяСтрока = СтрШаблон("Всего наименование %1 на сумму %2 KZT (%3)",
							КоличествоПозиций, СуммаИтого, СуммаПрописью);
													
        ОбластьИтоговаяСтрока.Параметры.Заполнить(Новый Структура("ИтоговаяСтрока", ИтоговаяСтрока));
		
		ТабДок.Вывести(ОбластьИтоговаяСтрока);
		ТабДок.Вывести(ОбластьПодписи); 
		
		ВставлятьРазделительСтраниц = Истина;
	КонецЦикла;
	
	Возврат ТабДок;
	
КонецФункции
